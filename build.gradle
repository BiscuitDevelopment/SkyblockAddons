import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
import groovy.json.*

plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version '2.1-SNAPSHOT'
    id 'io.franzbecker.gradle-lombok' version '3.3.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group = 'codes.biscuit'
// The below line is for version checkers <= 1.4.2
//version = "1.5.0"

// Java plugin settings
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "${project.minecraftVersion}-${project.forgeVersion}"
    runDir = "run"

    mappings = "${project.mappings}"

    // Replace placeholders in source code
    replace("@MOD_ID@": project.modId,
            "@MOD_NAME@": project.modName,
            "@VERSION@": project.version,
            "@MOD_ACCEPTED@": project.acceptedMinecraftVersions,
            "@UPDATE_JSON@": project.updateJSON)
    replaceIn("${project.modName}.java")
}

repositories {
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    // Fork of Discord RPC for Java https://github.com/BiscuitDevelopment/DiscordIPC
    implementation('com.github.BiscuitDevelopment:DiscordIPC:139c9d6') {
        exclude module: 'gson'
    }
}

jar {
    // Set the archive name here instead of in shadowJar because ForgeGradle needs it when re-obfuscating the jar.
    archiveName = project.modName + "-" + minecraft.version + "-" + version + ".jar"
    // Disable the default jar task and use shadowJar instead to shade the libraries into the jar.
    enabled = false
}

shadowJar {
    archiveName = tasks.jar.archiveName
    classifier = ''

    manifest.attributes(
            'Main-Class': 'OpenErrorMessage',
            'FMLCorePlugin': "${project.group}.${project.modId}.tweaker.${project.modName}LoadingPlugin",
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'ModSide': 'CLIENT',
            'FMLAT': "${project.modId}_at.cfg"
    )

    exclude('dummyThing')
    exclude('META-INF/maven/')
    exclude('META-INF/nar/')
    exclude('module-info.class')
    exclude('META-INF/versions/')

    minimize()
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// If you're in a dev environment, you need to run this task before running Minecraft.
task processResourcesDev(type: Copy) {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    into sourceSets.main.java.outputDir
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = project.group + '.' + modId + '.shadow'
}

shadowJar.dependsOn tasks.relocateShadowJar

// Updates the version JSON file automatically when the version changes
// Don't forget to write the changelog! Those aren't automatic.
task updateVersionsFile {
    def versionsFile = new File(".github/versions/dev-versions.json")

    def jsonBuilder = new JsonBuilder(new JsonSlurper().parse(versionsFile))
    def versionsMap = jsonBuilder.content."1.8.9"

    jsonBuilder.content.promos."1.8.9-latest" = version.toString()

    // Update the versions list if it doesn't have the current version.
    if (!versionsMap.containsKey(version)) {
        versionsMap.put(version, "<Insert Changelog Here>")
        versionsMap = versionsMap.sort(Comparator.reverseOrder())
        jsonBuilder.content."1.8.9" = versionsMap
    }

    versionsFile.write(jsonBuilder.toPrettyString())
}

tasks.build.dependsOn(updateVersionsFile)
tasks.reobfJar.dependsOn(tasks.shadowJar)

//reobf { // Only for labymod client.
//    jar {
//        useNotchSrg()
//    }
//}