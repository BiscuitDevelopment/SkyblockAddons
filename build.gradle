plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version 'FG_2.1-SNAPSHOT'
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'codes.biscuit'

// The below line is for version checkers <= 1.4.2
//version = "1.6.0"

// Java plugin settings
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
}

dependencies {
    // Discord RPC for Java https://github.com/jagrosh/DiscordIPC
    implementation('com.github.jagrosh:DiscordIPC:e29d6d8') {
        exclude module: 'log4j'
    }
    implementation('com.github.BiscuitDevelopment:HypixelLocalizationLib:7122472085') {
        exclude module: 'gson'
    }
    // Testing is unused for now
//    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

minecraft {
    version = "${project.minecraftVersion}-${project.forgeVersion}"
    runDir = "run"

    mappings = "${project.mappings}"

    // Replace placeholders in source code
    replace("@VERSION@": project.version,
            "@MOD_ACCEPTED@": project.acceptedMinecraftVersions)
    replaceIn("${project.modName}.java")
}

jar {
    // Set the archive name here instead of in shadowJar because ForgeGradle needs it when re-obfuscating the jar.
    archiveFileName = "${project.modName}-${project.version}-for-MC-${project.minecraftVersion}.jar"

    manifest.attributes(
            'Manifest-Version': '2.0',
            'Main-Class': 'SkyblockAddonsInstallerFrame',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' : 'BiscuitDevelopment',
            'Specification-Title': project.name,
            'Specification-Vendor': 'BiscuitDevelopment',
            'Specification-Version': project.version,
            'FMLCorePlugin': "${project.group}.${project.modId}.tweaker.${project.modName}LoadingPlugin",
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'ModSide': 'CLIENT',
            'FMLAT': "${project.modId}_at.cfg"
    )

    // Disable the default jar task and use shadowJar instead to shade the libraries into the jar.
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude('dummyThing')
    exclude('META-INF/maven/')
    exclude('META-INF/nar/')
    exclude('module-info.class')
    exclude('META-INF/versions/')

    // Relocate Discord RPC into the main codebase
    relocate 'com.jagrosh.discordipc', 'codes.biscuit.skyblockaddons.shadow.jagrosh.discordipc'
}

sourceJar {
    enabled = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task copyLicenses (type: Copy) {
    from(project.projectDir) {
        include('LICENSE')
        include('NOTICE')
        include('dependencyLicenses/**')
    }
    into(sourceSets.main.output.resourcesDir)
}

/*test {
    useJUnitPlatform()
}*/

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.jar.dependsOn(tasks.copyLicenses)
tasks.reobfJar.dependsOn(tasks.shadowJar)